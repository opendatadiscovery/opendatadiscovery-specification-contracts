plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.openapi.generator' version '5.2.0'
}

repositories {
    mavenCentral()
}

group = 'org.opendatadiscovery'
version = file('VERSION').text

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


dependencies {
    compileOnly "org.springframework.boot:spring-boot-starter-webflux:$springWebfluxVersion"

    compileOnly "javax.validation:validation-api:$validationApiVersion"
    compileOnly "javax.annotation:javax.annotation-api:$annotationApiVersion"
    compileOnly "io.swagger:swagger-annotations:$swaggerAnnotationsVersion"

    compileOnly "io.springfox:springfox-boot-starter:$springfoxBootStarterVersion"
    compileOnly "org.openapitools:jackson-databind-nullable:$jacksonDatabindVersion"
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

openApiGenerate {
    generatorName = "spring"

    inputSpec = "$projectDir/../opendatadiscovery-specification/specification/odd_adapter.yaml"
    outputDir = "$buildDir/generated"

    apiPackage = "org.opendatadiscovery.adapter.contract.api"
    invokerPackage = "org.opendatadiscovery.adapter.contract"
    modelPackage = "org.opendatadiscovery.adapter.contract.model"

    configOptions = [
            dateLibrary  : "java8",
            reactive     : "true",
            interfaceOnly: "true",
    ]

    globalProperties = [
            supportingFiles: "ApiUtil.java",
            apiTests       : "false",
            models         : "",
            apis           : ""
    ]
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar, javadocJar
}

signing {
    sign(publishing.publications)
}

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

    publications {
        contract(MavenPublication) {
            groupId = 'org.opendatadiscovery'
            artifactId = 'odd-platform-adapter-contract'
            version = version

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }

            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom {
                name = 'ODD Platform Ingestion Contract'
                description = 'ODD Platform Java package with OpenDataDiscovery models and APIs'
                url = 'https://github.com/opendatadiscovery/odd-platform-ingestion-contract'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'ndementev'
                        name = 'Nikita Dementev'
                        organization = 'opendatadiscovery.org'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/opendatadiscovery/odd-platform-ingestion-contract.git'
                    developerConnection = 'scm:git:git://github.com/opendatadiscovery/odd-platform-ingestion-contract.git'
                    url = 'https://github.com/opendatadiscovery/odd-platform-ingestion-contract'
                }
            }
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate
publish.dependsOn tasks.compileJava